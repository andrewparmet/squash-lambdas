plugins {
  id 'net.ltgt.errorprone-javacplugin' version '0.3'
  id 'com.github.spotbugs' version '1.6.2'
  id 'com.github.ben-manes.versions' version '0.17.0'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group = 'com.parmet'
version = '0.0.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'pmd'
apply plugin: 'com.github.spotbugs'
apply plugin: 'checkstyle'

spotbugs {
  visitors = [
    "SwitchFallthrough",
    "FindUselessObjects",
    "FindDeadLocalStores",
    "OptionalReturnNull",
    "FindUnsatisfiedObligation"
  ]
}

checkstyle {
  toolVersion = '8.8'
  maxWarnings = 0
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

pmd {
  ruleSetFiles = files("config/pmd/pmd.xml")
  toolVersion = '6.6.0'
}

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
}

dependencies {
  compile 'com.amazonaws:aws-java-sdk-s3:1.11.354'
  compile 'com.amazonaws:aws-java-sdk-sns:1.11.354'
  compile 'com.amazonaws:aws-lambda-java-core:1.2.0'  
  compile 'com.diffplug.durian:durian:3.4.0'
  compile 'com.fatboyindustrial.gson-javatime-serialisers:gson-javatime-serialisers:1.1.1'
  compile 'com.google.code.gson:gson:2.8.2'
  compile 'com.google.code.findbugs:annotations:3.0.1'
  compile 'com.google.guava:guava:25.1-jre'
  compile 'com.google.mug:mug:1.10'
  compile 'com.google.oauth-client:google-oauth-client-jetty:1.23.0'
  compile('com.google.apis:google-api-services-calendar:v3-rev305-1.23.0') {
    exclude group: 'com.google.guava'
  }
  compile 'net.sf.biweekly:biweekly:0.6.1'
  compile 'org.apache.commons:commons-email:1.5'
  compile 'org.apache.commons:commons-lang3:3.7'
  compile 'org.jsoup:jsoup:1.11.2'
  compile 'org.apache.commons:commons-configuration2:2.2'
  // commons-configuration2 does not pull this in on its own and
  // we need 1.9.3 to disable logging of an annoying stacktrace
  compile 'commons-beanutils:commons-beanutils:1.9.3'
  
  compile 'org.apache.logging.log4j:log4j-core:2.11.0'
  compile 'org.apache.logging.log4j:log4j-api:2.11.0'
  compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.0'
  runtime 'org.apache.logging.log4j:log4j-jcl:2.11.0'
  runtime 'commons-logging:commons-logging:1.2'

  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:3.9.1'
  testCompile 'com.google.truth:truth:0.40'
  testCompile 'com.google.truth.extensions:truth-java8-extension:0.40'
  testCompile fileTree(dir: 'src/test/lib', include: ['*.jar'])
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs = [
    '-Xlint:all',
    '-Xlint:-processing',
    '-Werror'
  ]
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

tasks.withType(Pmd) {
  it.ruleSets = []
}

task stage(dependsOn: [
  'checkstyleMain',
  'checkstyleTest',
  'spotbugsMain',
  'spotbugsTest',
  'pmdMain',
  'pmdTest',
  'compileTestJava',
  'shadowJar'
])
