plugins {
  id 'com.github.ben-manes.versions' version '0.25.0'
  id 'com.github.johnrengelman.shadow' version '5.1.0'
  id 'org.jetbrains.kotlin.jvm' version '1.3.50'
  id 'org.jlleitschuh.gradle.ktlint' version '8.2.0'
  id 'org.sonarqube' version '2.7.1'
  id 'jacoco'
}

group = 'com.parmet'
version = '0.0.1'

apply plugin: 'kotlin'
apply plugin: 'org.jlleitschuh.gradle.ktlint-idea'

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

repositories {
  mavenCentral()
  maven { url "https://plugins.gradle.org/m2/" }
  maven { url "https://artifactory.cronapp.io/public-release/" }
}

def awsSdkVersion = '1.11.676'
def log4jVersion = '2.12.1'
def kotlinVersion = '1.3.50'
def gsonVersion = '2.8.5'

dependencies {
  implementation "com.amazonaws:aws-java-sdk-dynamodb:$awsSdkVersion"
  implementation "com.amazonaws:aws-java-sdk-s3:$awsSdkVersion"
  implementation "com.amazonaws:aws-java-sdk-ses:$awsSdkVersion"
  implementation "com.amazonaws:aws-java-sdk-sns:$awsSdkVersion"
  implementation 'com.amazonaws:aws-lambda-java-core:1.2.0'

  implementation 'com.fatboyindustrial.gson-javatime-serialisers:gson-javatime-serialisers:1.1.1'
  implementation "com.google.code.gson:gson:$gsonVersion"
  implementation "com.google.code.gson:gson-extras:$gsonVersion"
  implementation 'com.google.guava:guava:28.1-jre'
  implementation 'com.google.auth:google-auth-library-oauth2-http:0.17.1'
  implementation('com.google.apis:google-api-services-calendar:v3-rev20190910-1.30.1') {
    exclude group: 'com.google.guava'
  }

  implementation 'net.sf.biweekly:biweekly:0.6.3'
  implementation 'org.apache.commons:commons-email:1.5'
  implementation 'org.jsoup:jsoup:1.12.1'
  implementation 'org.apache.commons:commons-configuration2:2.6'
  implementation 'commons-beanutils:commons-beanutils:1.9.4'
  implementation 'com.squareup.okhttp3:okhttp:4.2.0'
  implementation 'com.opencsv:opencsv:5.0'

  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  implementation 'io.github.microutils:kotlin-logging:1.7.6'

  implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
  implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
  implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
  implementation "org.apache.logging.log4j:log4j-jcl:$log4jVersion"
  implementation 'commons-logging:commons-logging:1.2'

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.assertj:assertj-core:3.13.2'
  testImplementation 'com.google.truth:truth:1.0'
  testImplementation 'org.reflections:reflections:0.9.11'
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs = [
    '-Xlint:all',
    '-Xlint:-processing',
    '-Werror'
  ]
}

task stage(dependsOn: [
  'ktlintCheck',
  'shadowJar'
])

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

jacoco {
  toolVersion = '0.8.3'
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

sonarqube {
  properties {
    property 'sonar.projectKey', 'squash-lambdas'
    property 'sonar.organization', 'andrewparmet-github'
    property 'sonar.host.url', 'https://sonarcloud.io'
    property 'sonar.coverage.jacoco.xmlReportPaths', "${projectDir}/build/reports/jacoco/test/jacocoTestReport.xml"
  }
}
